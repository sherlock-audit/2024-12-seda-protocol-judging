Passive Latte Sealion

High

# Submitting requests could be prevented by front-running `postRequest` and submitting same exact request

### Summary

The `SedaCoreV1::postRequest` function allows any address to submit a request in `SedaCoreV1`, which will eventually be picked up by solvers and forwarded to the SEDA chain for execution. `SedaCoreV1` stores these requests by mapping the request ID to the request content and prevents a request from being submitted twice. However, because all request parameters can be chosen arbitrarily, a malicious actor can repeatedly submit an identical request before requester—with zero `resultFee`, `batchFee`, and `requestFee`—and prevent the victim from submitting their request. The impact is more significant on L2s, where lower gas prices make this attack almost cost-free.

### Root Cause

In `RequestHandlerBase.sol::postRequest`, the same request ID (derived from the inputs fields) cannot be submitted again, as shown here (https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/core/abstract/RequestHandlerBase.sol#L41-L44):
```solidity
        bytes32 requestId = SedaDataTypes.deriveRequestId(inputs);
        //@audit do not let re-submission of same request
        if (bytes(_requestHandlerStorage().requests[requestId].version).length != 0) {
            revert RequestAlreadyExists(requestId);
        }
```
looking at `deriveRequestId` implementation, all the parameters can be replicated by attacker:
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/libraries/SedaDataTypes.sol#L122-L139
```solidity
    function deriveRequestId(RequestInputs memory inputs) internal pure returns (bytes32) {
        return
            keccak256(
                bytes.concat(
                    keccak256(bytes(SedaDataTypes.VERSION)),
                    inputs.execProgramId,
                    keccak256(inputs.execInputs),
                    bytes8(inputs.execGasLimit),
                    inputs.tallyProgramId,
                    keccak256(inputs.tallyInputs),
                    bytes8(inputs.tallyGasLimit),
                    bytes2(inputs.replicationFactor),
                    keccak256(inputs.consensusFilter),
                    bytes16(inputs.gasPrice),
                    keccak256(inputs.memo)
                )
            );
    }
```
in other words, none of the above fields are tied to `msg.sender`, meaning that anyone is able to front-run and submit same request which yields same request id (With the only difference being that fees are set to 0 to prevent solvers from picking the request)

### Internal Pre-conditions
- `SedaCoreV1` must not be in `paused` state

### External Pre-conditions
n/a

### Attack Path
1. A protocol that lends ETH using a token with a 1:1 ratio to gold attempts to fetch the price of gold from SEDA.
The protocol contract calls `SedaCoreV1::postRequest` with parameters to invoke the oracle program responsible for fetching the commodity asset price. 
2. The request (`postRequest` transaction) enters the mempool but is not yet included in any blocks.
3. An attacker, who holds a large amount of gold as collateral, is aware that the gold price has dropped and that a new price update could render his position liquidatable.
4. The attacker continuously prevents the protocol from obtaining the gold price from the SEDA chain by submitting an identical request with zero `resultFee`, `batchFee`, and `requestFee`, causing the request to be ignored by the solvers.

### Impact
1. The victim can change the memo and resubmit the request. On Ethereum mainnet, this would cost the attacker the gas fee, and the victim might eventually win the submission race. However, on chains like Arbitrum or Optimism, where gas fees are lower, the attack becomes nearly free for the attacker, potentially forcing the victim to pay high gas fees to get their request through.
2. Some services might use a fixed memo field for all requests (e.g., as an identifier to filter their requests from on-chain events). This means that an attacker can front-run those requests easily until those services choose a different memo.
3. Some services might require critical and time-sensitive data. Experiencing this type of service interruption could severely impact these services and their customers (and eventually the SEDA protocol) by causing delays or failures in data retrieval.

### PoC
-- no response --

### Mitigation
One possible solution is to include `msg.sender` in the request hash. For example:
```solidity
    //@audit change to `view`
    function deriveRequestId(RequestInputs memory inputs) internal view returns (bytes32) {
        return
            keccak256(
                bytes.concat(
                    keccak256(bytes(SedaDataTypes.VERSION)),
                    inputs.execProgramId,
                    keccak256(inputs.execInputs),
                    bytes8(inputs.execGasLimit),
                    inputs.tallyProgramId,
                    keccak256(inputs.tallyInputs),
                    bytes8(inputs.tallyGasLimit),
                    bytes2(inputs.replicationFactor),
                    keccak256(inputs.consensusFilter),
                    bytes16(inputs.gasPrice),
                    keccak256(inputs.memo),
                    //@audit-fix include msg.sender to make the request id unique
                    bytes20(msg.sender)
                )
            );
    }
```