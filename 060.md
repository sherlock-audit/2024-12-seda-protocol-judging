Sweet Sky Worm

Medium

# feeTransfer failing for any receiver would DoS `postResult` allowing the `requestor` to reclaim all fees through refund for request which has been served.

`PostRequest` allows a `requestor` to make a request with fees in-order to incentivize others to fill the request.
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/core/SedaCoreV1.sol#L99
```solidity
function postRequest(
        SedaDataTypes.RequestInputs calldata inputs,
        uint256 requestFee,
        uint256 resultFee,
        uint256 batchFee
    ) public payable whenNotPaused returns (bytes32) {
        // Validate that the sent ETH matches exactly the sum of all specified fees
        // This prevents users from accidentally overpaying or underpaying fees
@>      if (msg.value != requestFee + resultFee + batchFee) {
            revert InvalidFeeAmount();
        }

        ....

        // Store pending request and request details
        _addRequest(requestId);
        _storageV1().requestDetails[requestId] = RequestDetails({
@>          requestor: msg.sender,
            timestamp: block.timestamp,
            requestFee: requestFee,
            resultFee: resultFee,
            batchFee: batchFee,
            gasLimit: inputs.execGasLimit + inputs.tallyGasLimit
        });

        return requestId;
    }
```
The issue is that fee payment are done through a push method where every party would have to receive the fee or it would revert.
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/core/SedaCoreV1.sol#L197
```solidity
function postResult(
        SedaDataTypes.Result calldata result,
        uint64 batchHeight,
        bytes32[] calldata proof
    ) public payable override(ResultHandlerBase, IResultHandler) whenNotPaused returns (bytes32) {
        RequestDetails memory requestDetails = _storageV1().requestDetails[result.drId];

        ...

        // Call parent contract's postResult implementation and retrieve both the result ID
        // and the batch sender address for subsequent fee distribution logic
        (bytes32 resultId, address batchSender) = super.postResultAndGetBatchSender(result, batchHeight, proof);

        // Clean up state
        _removePendingRequest(result.drId);
        delete _storageV1().requestDetails[result.drId];

        // Fee distribution: handles three types of fees (request, result, batch)
        // and manages refunds back to the requestor when applicable

        // Amount to refund to requestor
        uint256 refundAmount;

        // Request fee distribution:
        // - if invalid payback address, send all request fee to requestor
        // - if valid payback address, split request fee proportionally based on gas used vs gas limit
        if (requestDetails.requestFee > 0) {
            address payableAddress = result.paybackAddress.length == 20
                ? address(bytes20(result.paybackAddress))
                : address(0);

            if (payableAddress == address(0)) {
 @>            refundAmount += requestDetails.requestFee;
            } else {
                // Split request fee proportionally based on gas used vs gas limit
                uint256 submitterFee = (result.gasUsed * requestDetails.requestFee) / requestDetails.gasLimit;
                if (submitterFee > 0) {
 @>                 _transferFee(payableAddress, submitterFee);
                    emit FeeDistributed(result.drId, payableAddress, submitterFee, ISedaCore.FeeType.REQUEST);
                }
                refundAmount += requestDetails.requestFee - submitterFee;
            }
        }

        // Result fee distribution:
        // - send all result fee to `msg.sender` (result sender/solver)
        if (requestDetails.resultFee > 0) {
 @>          _transferFee(msg.sender, requestDetails.resultFee);
            emit FeeDistributed(result.drId, msg.sender, requestDetails.resultFee, ISedaCore.FeeType.RESULT);
        }

        // Batch fee distribution:
        // - if no batch sender, send all batch fee to requestor
        // - if valid batch sender, send batch fee to batch sender
        if (requestDetails.batchFee > 0) {
            if (batchSender == address(0)) {
                // If no batch sender, send all batch fee to requestor
                refundAmount += requestDetails.batchFee;
            } else {
                // Send batch fee to batch sender
                _transferFee(batchSender, requestDetails.batchFee);
                emit FeeDistributed(result.drId, batchSender, requestDetails.batchFee, ISedaCore.FeeType.BATCH);
            }
        }

        
        if (refundAmount > 0) {
@>           _transferFee(requestDetails.requestor, refundAmount);
            emit FeeDistributed(result.drId, requestDetails.requestor, refundAmount, ISedaCore.FeeType.REFUND);
        }

        return resultId;
    }
```
`_transferFee()` would revert if there's failure for any payment recipient. this state can be reached either any of the recipient is a contract without `receive payable` or the recipient reverts the transaction on reciept.
```solidity
function _transferFee(address recipient, uint256 amount) internal {
        // Using low-level call instead of transfer()
        (bool success, ) = payable(recipient).call{value: amount}("");
        if (!success) revert FeeTransferFailed();
    }
```
The `requestor` can intentionally revert the initial `refund` in order to claim the `totalRefund` through `withdrawTimedOutRequest`.
## Impact
Requests which have been served can be DoS from `PostResult` denying the both `submitter` and `batchSender` or any party there entitled fees.
## Recommendation
Store fees separately for each party so the can claim them later. Use a pull method for sending fees.