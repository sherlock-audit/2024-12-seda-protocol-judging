Passive Latte Sealion

High

# `Secp256k1ProverV1` is vulnerable to cross-chain replay attacks

### Summary

The `Secp256k1ProverV1::postBatch` function allows the submission of a new batch as long as it is signed by validators included in the latest validators root, verified by the `validatorsProof` argument. If different chains share the same validators root—**which is likely**—the same `signatures` and `validatorsProof` can be used to submit a batch from one chain to another. This behavior may be unintended, as different chains have different requests, results, and results roots.

### Root Cause

To accept a new batch, `postBatch` performs this validations and checks:
**1- Check that new batch height is greater than the previous one:**
```solidity
        // Prevents replay attacks via strictly ordered batches
        if (newBatch.batchHeight <= s.lastBatchHeight) {
            revert InvalidBatchHeight();
        } 
```
This check can be bypassed if the replayed batch has a higher number, which is entirely possible.


**2- Derive batch id for verification**
```solidity
        bytes32 batchId = SedaDataTypes.deriveBatchId(newBatch);
```
the batch ID is derived for signature verification as follows:
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/libraries/SedaDataTypes.sol#L106-L117
```solidity
    function deriveBatchId(Batch memory batch) internal pure returns (bytes32) {
        return
            keccak256(
                bytes.concat(
                    bytes8(batch.batchHeight),
                    bytes8(batch.blockHeight),
                    batch.validatorsRoot,
                    batch.resultsRoot,
                    batch.provingMetadata
                )
            );
    }
```
Notice that no chain-specific data (e.g., chain ID) is included. As a result, using batch data from a different chain yields the same batch ID.


**3- Confirm that all validators who signed the batch are included in the last validators root** 
```solidity
        for (uint256 i = 0; i < validatorProofs.length; i++) {
            // ...
            if (!_verifyValidatorProof(validatorProofs[i], s.lastValidatorsRoot)) {
                revert InvalidValidatorProof();
            }
            // ...
        }
```
Since different chains share the same validators root (unless there is a submission delay causing the validator set to be different) we can expect `_verifyValidatorProof` to be able to verify that all the validators are included in the last validators root:
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol#L112-L114
```solidity
    function _verifyValidatorProof(
        SedaDataTypes.ValidatorProof memory proof,
        bytes32 validatorsRoot
    ) internal pure returns (bool) {
        bytes32 leaf = keccak256(abi.encodePacked(SECP256K1_DOMAIN_SEPARATOR, proof.signer, proof.votingPower));

        return MerkleProof.verify(proof.merkleProof, validatorsRoot, leaf);
    }
```
**4-  Confirm that the batch ID is signed by each validator:**
```solidity
        for (uint256 i = 0; i < validatorProofs.length; i++) {
            // ...
            // Verify signature is valid and signed by the validator
            if (!_verifySignature(batchId, signatures[i], validatorProofs[i].signer)) {
                revert InvalidSignature();
            }
            //...
        }
```
`_verifySignature` confirms that validator has signed `messageId` which is the `batchId` we have derived earlier and we saw it does not contain any chain-specific fields:
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol#L210-L216
```solidity
    function _verifySignature(
        bytes32 messageHash, //@audit this is the batch id
        bytes calldata signature,
        address signer
    ) internal pure returns (bool) {
        return ECDSA.recover(messageHash, signature) == signer;
    }
```

**5- The function checks that the sum of the validators' voting power exceeds 66.6% (the consensus threshold):** 
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol#L122-L125
```solidity
        // Check that voting power meets or exceeds the consensus threshold (2/3)
        if (votingPower < CONSENSUS_PERCENTAGE) {
            revert ConsensusNotReached();
        }
```

Finally, the state is updated:
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol#L127-L132
```solidity
        // After consensus is reached, commit the new batch and update validator set
        // This establishes the new state for future batch validations
        s.lastBatchHeight = newBatch.batchHeight;
        s.lastValidatorsRoot = newBatch.validatorsRoot;
        s.batches[newBatch.batchHeight] = BatchData({resultsRoot: newBatch.resultsRoot, sender: msg.sender});
        emit BatchPosted(newBatch.batchHeight, batchId, msg.sender);
```

### Internal Pre-conditions

- Contract must not be in `paused` state
- `newBatch.batchHeight > s.lastBatchHeight`
- `s.lastValidatorsRoot` must contain all current validators
- Voting power of batch id signers must be greater than `CONSENSUS_PERCENTAGE`

### External Pre-conditions

- To submit the new batch on another chain, validators must submit the batch on current chain and batch height of target chain must be less than current one

### Attack Path

1. Validators submit batch number 5 on ethereum, which contains the results root of all 100 requests made to SEDA chain
2. Attacker notices that batch height of optimism is less than ethereum one, he replays the ethereum transaction on optimism
3. Since the batch Id on optimism is 4, and the new batch height is 5, all the checks are passed and the batch is submitted successfully
4. Validators try to submit the batch number 5 on optimism which contains the results to requests of applications hosted on optimism, but the transaction reverts because current batch height is already 5

### Impact

- All the results to requests made on the target chain (optimism in example above) can not be submitted again, as all the data results are market as batched on seda chain batching module:
```go
func (k Keeper) MarkDataResultAsBatched(ctx context.Context, result types.DataResult, batchNum uint64) error {
	err := k.SetBatchAssignment(ctx, result.DrId, result.DrBlockHeight, batchNum)
	if err != nil {
		return err
	}
	err = k.dataResults.Remove(ctx, collections.Join3(false, result.DrId, result.DrBlockHeight))
	if err != nil {
		return err
	}
	return k.dataResults.Set(ctx, collections.Join3(true, result.DrId, result.DrBlockHeight), result)
}
```
this results can not be fetched again in the endblocker of batching module
- The order of batches in `Secp256k1ProverV1` is disrupted
- All the batches between the current batch on target chain and the new submitted batch are lost

### PoC

_No response_

### Mitigation

When calculating the batch id, also include the chainid, however, this might require changes in other parts of seda codebase as well.
```solidity
    function deriveBatchId(Batch memory batch) internal pure returns (bytes32) {
        return
            keccak256(
                bytes.concat(
                    bytes8(batch.batchHeight),
                    bytes8(batch.blockHeight),
                    batch.validatorsRoot,
                    batch.resultsRoot,
                    batch.provingMetadata,
                   //@audit add a chainid
                   (block.chainid)
                )
            );
    }
```
The other solution is to add EIP-712 signing schema to `Secp256k1ProverV1` when verifying the validators signature