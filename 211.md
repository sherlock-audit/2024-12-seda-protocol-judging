Passive Latte Sealion

Medium

# `SedaCoreV1` does not check whether the result still exist in `pendingRequests` and `requestDetails`

### Summary
The postResult function does not verify whether the corresponding request still exists in the `pendingRequests` and  `requestDetails`. Consequently, if a request is withdrawn prior to result posting, the fee distribution will not occur—meaning that the solver will not receive their fee shares. This opens an attack vector where a malicious user can exploit the issue to obtain free results. (discussed in impacts section)

### Root Cause
The vulnerability occurs because `postResult` retrieves request details from storage without checking if the request remains active and still exists in `requestDetails`. Here’s the detailed flow:

**1. Fetching request details:**
The function retrieves the request details from the `_storageV1().requestDetails` mapping:
```solidity
        RequestDetails memory requestDetails = _storageV1().requestDetails[result.drId];
```
**2. Timestamp validation:**
It then checks that `result.blockTimestamp` is greater than the stored request timestamp:
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/core/SedaCoreV1.sol#L131-L134
```solidity
        // Note: Validation always passes for non-tracked requests (where requestDetails.timestamp is 0)
        if (result.blockTimestamp <= requestDetails.timestamp) {
            revert InvalidResultTimestamp(result.drId, result.blockTimestamp, requestDetails.timestamp);
        }
```
According to the documentation, if `requestDetails` does not exist (i.e., its timestamp is zero), this condition still passes. This means that even if the request was withdrawn (using `withdrawTimedOutRequest`) and removed from `pendingRequests` and `requestDetails`, the check does not revert the transaction.

**3. Processing the Result:**
The function then passes the result parameter to `postResultAndGetBatchSender`:
```solidity
        // Call parent contract's postResult implementation and retrieve both the result ID
        // and the batch sender address for subsequent fee distribution logic
        (bytes32 resultId, address batchSender) = super.postResultAndGetBatchSender(result, batchHeight, proof);
```
Within `postResultAndGetBatchSender`, the only validation performed is to ensure that `result.drId` is included in the batch's `resultsRoot`. Specifically:
```solidity
function postResultAndGetBatchSender(
    SedaDataTypes.Result calldata result,
    uint64 batchHeight,
    bytes32[] calldata proof
) internal returns (bytes32, address) {
    bytes32 resultId = SedaDataTypes.deriveResultId(result);
    // The result must not already be set.
    if (_resultHandlerStorage().results[result.drId].drId != bytes32(0)) {
        revert ResultAlreadyExists(result.drId);
    }
    // The resultId must exist in the batch's results root.
    (bool isValid, address batchSender) = _resultHandlerStorage().sedaProver.verifyResultProof(
        resultId,
        batchHeight,
        proof
    );
    // The result must be valid.
    if (!isValid) {
        revert InvalidResultProof(resultId);
    }
    // Finally, assign the result.
    _resultHandlerStorage().results[result.drId] = result;
    emit ResultPosted(resultId);
    return (resultId, batchSender);
}
```
This code confirms that the result is part of the batch but does not verify that the original request is still active and in the storage. As a result, if the request has been withdrawn prior to result posting, the result will be accepted and recorded. However, subsequent fee distribution logic—relying on the now-deleted `requestDetails`—will distribute zero fees to the submitter, solver, and batch sender.
next in `postResult` we are deleting the non-existent result again:
```solidity
        // Clean up state
        _removePendingRequest(result.drId);
        delete _storageV1().requestDetails[result.drId];
```
and then we are sending the fees, since `requestDetails` was deleted from storage before, then all fees are zero:
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/core/SedaCoreV1.sol#L153-L199

### Internal Pre-conditions
- The contract must not be in a `paused` state.

### External Pre-conditions
- At the time when solver picked up the request, it was still not expired.
- The result get expired **AFTER** the solver relayed the results to the chain

### Attack Path
**One possible attack path exploiting this vulnerability is as follows:**
1. A malicious user submits a request with zero fees.
2. The malicious user waits until the request is near expiration (to ensure that its picked up by solvers, but when the result is received its expired)
3. The malicious user then calls `increaseFees` with a large fee amount to attract solvers.
4. The request is executed by the SEDA chain, and its result is included in a batch.
5. Before a solver posts the result using `postResult`, the attacker front-runs the transaction by calling `withdrawTimedOutRequest`, withdrawing the fees and removing request from pending requests.
6. The result is still posted, but fee distribution does not happen because the corresponding `requestDetails` no longer exists, allowing the attacker to effectively **receive the result for free.**

### Impact
- The result might be posted even after the corresponding request has been withdrawn.
- Fee distribution to the solver will not happen, **allowing malicious users to obtain results without paying the required fees.**

### PoC

_No response_

### Mitigation
To mitigate this issue, the contract should revert if the request does not exist in the `requestDetails`. One straightforward approach is to update the timestamp validation to also check for a zero timestamp, which indicates that the request has been withdrawn (or not exist). For example:
```solidity
        if (result.blockTimestamp <= requestDetails.timestamp || requestDetails.timestamp == 0) {
            revert InvalidResultTimestamp(result.drId, result.blockTimestamp, requestDetails.timestamp);
        }
```